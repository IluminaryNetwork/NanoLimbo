plugins {
    id 'java'
    id 'com.gradleup.shadow' version '8.3.6'
    id 'com.github.gmazzo.buildconfig' version '3.1.0'
}

group 'ua.nanit'
version '1.9.3'

compileJava {
    options.encoding = "UTF-8"
}

tasks.withType(JavaCompile).configureEach {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.13.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.13.1'

    implementation 'ch.qos.logback:logback-classic:1.5.18'
    implementation 'org.spongepowered:configurate-yaml:4.2.0'

    implementation "io.netty:netty-handler:4.2.2.Final"
    implementation "io.netty:netty-transport-native-epoll:4.2.2.Final:linux-x86_64"
    implementation "io.netty:netty-transport-native-epoll:4.2.2.Final:linux-aarch_64"

    implementation 'net.kyori:adventure-nbt:4.22.0'
    implementation 'com.grack:nanojson:1.10'
    implementation 'com.google.code.gson:gson:2.13.1'
}

buildConfig {
    className("BuildConfig")
    packageName("ua.nanit.limbo")
    buildConfigField('String', 'LIMBO_VERSION', "\"${project.version}\"")
}

shadowJar {
    from 'LICENSE'

    manifest {
        attributes('Main-Class': 'ua.nanit.limbo.NanoLimbo')
    }

    minimize {
        exclude(dependency('ch.qos.logback:logback-classic:.*:.*'))
    }
}

test {
    useJUnitPlatform()
}
